#lang sicp

(define (make-connector)
  (let ((value #f) (informant #f) (constraints '()))
    (define (set-value val setter)
      (cond
        ((not (has-value? me))
          (set! value val)
          (set! informant setter)
          (for-each-except setter inform-about-value constraints))
        ((not (= value val))
          (error "contradiction" (list value val)))
        (else 'ignored)))
    (define (forget-value retractor)
      (if (eq? informant retractor)
        (begin
          (set! informant #f)
          (for-each-except retractor inform-about-no-value constraints))
        'ignored))
    (define (connect new-constraint)
      (if (not (memq new-constraint constraints))
        (set! constraints (cons new-constraint constraints)))
      (if (has-value? me)
        (inform-about-value new-constraint))
      'done)
    (define (me req)
      (cond
        ((eq? req 'has-value?)
          (if informant #t #f))
        ((eq? req 'value) value)
        ((eq? req 'set-value!) set-value)
        ((eq? req 'forget) forget-value)
        ((eq? req 'connect) connect)
        (else (error "Unknown operation -- CONNECTOR" req))))
    me))

(define (for-each-except exception proc list)
  (define (loop items)
    (cond
      ((null? items) 'done)
      ((eq? (car items) exception) (loop (cdr items)))
      (else
        (proc (car items))
        (loop (cdr items)))))
  (loop list))

(define (has-value? connector)
  (connector 'has-value?))

(define (get-value connector)
  (connector 'value))

(define (set-value! connector val informant)
  ((connector 'set-value!) val informant))

(define (forget-value! connector retractor)
  ((connector 'forget) retractor))

(define (connect connector new-constraint)
  ((connector 'connect) new-constraint))

(define (inform-about-value c)
  (c 'I-have-a-value))

(define (inform-about-no-value c)
  (c 'I-lost-my-value))

(define (probe name connector)
  (define (print-probe value)
    (newline)
    (display "Probe: ")
    (display name)
    (display " = ")
    (display value))
  (define (process-new-value)
    (print-probe (get-value connector)))
  (define (process-forget-value)
    (print-probe "?"))
  (define (me req)
    (cond
      ((eq? req 'I-have-a-value) (process-new-value))
      ((eq? req 'I-lost-my-value) (process-forget-value))))
  (connect connector me)
  me)

(define (squarer a b)
  (define (process-new-value)
    (if (has-value? b)
      (if (< (get-value b) 0)
        (error "square less than 0 -- SQUARER" (get-value b))
        (set-value! a (sqrt (get-value b)) me))
      (if (has-value? a)
        (set-value! b (* (get-value a) (get-value a)) me)
        'ignored)))
  (define (process-forget-value)
    (forget-value! a me)
    (forget-value! b me)
    (process-new-value))
  (define (me req)
    (cond
      ((eq? req 'I-have-a-value) (process-new-value))
      ((eq? req 'I-lost-my-value) (process-forget-value))
      (else (error "unknown req"))
      ))
  (connect a me)
  (connect b me)
  me)

(define a (make-connector))
(define b (make-connector))
(define sq (squarer a b))
(probe "a" a)
(probe "b" b)
(set-value! a 4 'user)
(forget-value! a 'user)
(set-value! b 100 'user)
